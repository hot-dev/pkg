::resend::api ns
meta {ctx: {"resend.api.key": {required: true}}}

// Namespace alias for ctx module
::ctx ::hot::ctx

http-request ::hot::http/request
is-ok-response ::hot::http/is-ok-response
HttpResponse ::hot::http/HttpResponse

HttpError type {
    status: Int,
    headers: Map,
    body: Any
}

HttpResponse -> HttpError fn (response: HttpResponse): HttpError {
  HttpError({
    status: response.status,
    headers: response.headers,
    body: response.body
  })
}

request
meta {
    doc: "Make an HTTP request with pre-filled service headers. Additional headers can be provided to merge/override defaults."
}
fn
(method: Str, url: Str, additional-headers: Map, body: Any): HttpResponse {
  default-headers {
    Authorization: `Bearer ${::ctx/get("resend.api.key")}`
  }

  // Add Content-Type for requests with bodies
  headers if(eq(body, ""), default-headers, merge(default-headers, {Content-Type: "application/json"}))

  // Merge in any additional headers (they can override defaults)
  final-headers merge(headers, additional-headers)

  ::hot::http/request(method, url, final-headers, to-json(untype(body)))
},
(method: Str, url: Str, additional-headers: Map): HttpResponse {
  request(method, url, additional-headers, "")
},
(method: Str, url: Str): HttpResponse {
  request(method, url, {}, "")
}

::anthropic::api ns
meta {ctx: {"anthropic.api.key": {required: true}}}

// Namespace alias for ctx module
::ctx ::hot::ctx

http-request ::hot::http/request
http-request-stream ::hot::http/request-stream
is-ok-response ::hot::http/is-ok-response
HttpResponse ::hot::http/HttpResponse
StreamingHttpResponse ::hot::http/StreamingHttpResponse

HttpError type {
    status: Int,
    headers: Map,
    body: Any
}

HttpResponse -> HttpError fn (response: HttpResponse): HttpError {
  HttpError({
    status: response.status,
    headers: response.headers,
    body: response.body
  })
}

request
meta {
    doc: "Make an HTTP request with pre-filled service headers. Additional headers can be provided to merge/override defaults."
}
fn
(method: Str, url: Str, additional-headers: Map, body: Any): HttpResponse {
  default-headers {
    x-api-key: ::ctx/get("anthropic.api.key"),
    anthropic-version: "2023-06-01"
  }

  // Add Content-Type for requests with bodies
  headers if(eq(body, ""), default-headers, merge(default-headers, {Content-Type: "application/json"}))

  // Merge in any additional headers (they can override defaults)
  final-headers merge(headers, additional-headers)

  ::hot::http/request(method, url, final-headers, to-json(untype(body)))
},
(method: Str, url: Str, additional-headers: Map): HttpResponse {
  request(method, url, additional-headers, "")
},
(method: Str, url: Str): HttpResponse {
  request(method, url, {}, "")
}

request-stream
meta {
    doc: "Make a streaming HTTP request with pre-filled service headers. Returns a response with an iterator body for SSE/NDJSON streams."
}
fn
(method: Str, url: Str, additional-headers: Map, body: Any, format: Str): StreamingHttpResponse {
  default-headers {
    x-api-key: ::ctx/get("anthropic.api.key"),
    anthropic-version: "2023-06-01"
  }

  // Add Content-Type for requests with bodies
  headers if(eq(body, ""), default-headers, merge(default-headers, {Content-Type: "application/json"}))

  // Merge in any additional headers (they can override defaults)
  final-headers merge(headers, additional-headers)

  ::hot::http/request-stream(method, url, final-headers, to-json(untype(body)), format)
},
(method: Str, url: Str, additional-headers: Map, body: Any): StreamingHttpResponse {
  request-stream(method, url, additional-headers, body, "sse")
}
